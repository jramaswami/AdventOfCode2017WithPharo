# Day 6: Memory Reallocation

I had a difficult time on this puzzle.  First, I somehow broke my image.  It
took me several hours of Google-ing and fussing around before I was able to
restore things.  After that I got stuck on some unexpected behavior; more on
that below.

I also figured out some new features in Pharo.  I found the automatic
generation of attribute accessors very handy.  I also like the way you can 
Create a Test and Jump.  The test is then connected to the method and you
can run the tests as you make changes.  Very nice!  They say that Smalltalk
changes the way you program -- I can see what they mean.

## Solution to Part 1

The solutions for this puzzle are quite intertwined. And it wasn't really
much extra work to solve Part 2 once Part 1 is solved.  Basically I use
a Dictionary to keep track of the different memory states.  You can then
loop over the memory reallocations until you run into a memory state that
has been seen before.  This gives you the answer to both parts of the puzzle.

The Day6Solver has instance variables to track the memory blocks, the memory
states that have been seen, and the time (or the number of cycles).

I built the solution up from the bottom.  First, we have to find the index
of the memory block with the most blocks.

```smalltalk
Day6Solver >> getMaxMemoryIndex
	| maxVal maxIndex |
	maxVal := 0.
	maxIndex := 0.
	self memoryBanks
		withIndexDo: [ :val :index | 
			val > maxVal
				ifTrue: [ maxVal := val.
					maxIndex := index ] ].
	^ maxIndex
```

Once you have the right memory you have to be able to redistribute its blocks.

```smalltalk
Day6Solver >> reallocationCycle
	| index memoryToRealloc |
	index := self getMaxMemoryIndex.
	memoryToRealloc := self memoryBanks at: index.
	self memoryBanks at: index put: 0.
	[ memoryToRealloc > 0 ]
		whileTrue: [ index := index + 1.
			index > self memoryBanks size
				ifTrue: [ index := 1 ].
			self memoryBanks at: index put: (self memoryBanks at: index) + 1.
			memoryToRealloc := memoryToRealloc - 1 ].
```

Now that you've done that you need to loop through cycles until you see
a memory state that has been seen before.

```smalltalk
Day6Solver >> debugReallocation
	self memoryStates: Dictionary new.
	[ (self memoryStates) includesKey: (self memoryBanks asString) ] whileFalse: [ 
		self memoryStates at: (self memoryBanks asString) put: (self time).
		self time: (self time) + 1.
		self reallocationCycle.
	 ]
```

Now we can get the solution to the problem.

```smalltalk
Day6Solver >> solveA: anInput
	self initialize: anInput.
	self debugReallocation.
	^ self cyclesBeforeInfiniteLoop 
```

```smalltalk
Day6Solver >> cyclesBeforeInfiniteLoop
	^ self time
```

## Solution to Part 2

The solution to Part 2 follows from part 1.

```smalltalk
Day6Solver >> solveB: anInput
	self initialize: anInput.
	self debugReallocation.
	^ self cyclesInInfiniteLoop 
```

```smalltalk
Day6Solver >> cyclesInInfiniteLoop
	^ self time - (self memoryStates at: self memoryBanks asString)
```

## Some Behavior I Don't Understand

I ran into a problem with using a Dictionary to keep track of the different
memory states.  I thought I could use the OrderedCollection or an array as
the key to the Dictionary.  However, when I tried this it produces some
unexpected behavior.  When the solver's memoryBanks is updated and then used
to add a memory state to the Dictionary, all the entries in the Dictionary
are updated.  See the Transcript.

```smalltalk
dict := Dictionary new.
solver := Day6Solver new.
solver initialize: (#(0 2 7 0) asOrderedCollection).
t := 1.
[ dict includesKey: (solver memoryBanks) ] whileFalse: [
	dict at: (solver memoryBanks) put: t.
	Transcript show: (solver memoryBanks); cr.
	Transcript show: dict; cr.
	t := t + 1.
	solver reallocationCycle. ].
Transcript show: dict; cr.
```

Transcript:
```
an OrderedCollection(0 2 7 0)
a Dictionary(an OrderedCollection(0 2 7 0)->1 )
an OrderedCollection(2 4 1 2)
a Dictionary(an OrderedCollection(2 4 1 2)->2 an OrderedCollection(2 4 1 2)->2 )
an OrderedCollection(3 1 2 3)
a Dictionary(an OrderedCollection(3 1 2 3)->3 an OrderedCollection(3 1 2 3)->3 an OrderedCollection(3 1 2 3)->3 )
a Dictionary(an OrderedCollection(0 2 3 4)->2 an OrderedCollection(0 2 3 4)->2 an OrderedCollection(0 2 3 4)->2 )
```

However, changing the OrderedCollection to a string produces the expected
behavior.

```smalltalk
dict := Dictionary new.
solver := Day6Solver new.
solver initialize: (#(0 2 7 0) asOrderedCollection).
t := 1.
[ dict includesKey: (solver memoryBanks asString) ] whileFalse: [
	dict at: (solver memoryBanks asString) put: t.
	Transcript show: (solver memoryBanks); cr.
	Transcript show: dict; cr.
	t := t + 1.
	solver reallocationCycle. ].
Transcript show: dict; cr.
```

Transcript:
```
an OrderedCollection(0 2 7 0)
a Dictionary('an OrderedCollection(0 2 7 0)'->1 )
an OrderedCollection(2 4 1 2)
a Dictionary('an OrderedCollection(0 2 7 0)'->1 'an OrderedCollection(2 4 1 2)'->2 )
an OrderedCollection(3 1 2 3)
a Dictionary('an OrderedCollection(0 2 7 0)'->1 'an OrderedCollection(2 4 1 2)'->2 'an OrderedCollection(3 1 2 3)'->3 )
an OrderedCollection(0 2 3 4)
a Dictionary('an OrderedCollection(0 2 3 4)'->4 'an OrderedCollection(0 2 7 0)'->1 'an OrderedCollection(2 4 1 2)'->2 'an OrderedCollection(3 1 2 3)'->3 )
an OrderedCollection(1 3 4 1)
a Dictionary('an OrderedCollection(0 2 3 4)'->4 'an OrderedCollection(0 2 7 0)'->1 'an OrderedCollection(1 3 4 1)'->5 'an OrderedCollection(2 4 1 2)'->2 'an OrderedCollection(3 1 2 3)'->3 )
a Dictionary('an OrderedCollection(0 2 3 4)'->4 'an OrderedCollection(0 2 7 0)'->1 'an OrderedCollection(1 3 4 1)'->5 'an OrderedCollection(2 4 1 2)'->2 'an OrderedCollection(3 1 2 3)'->3 )
```

In Python this typically means that a reference is being used where you did 
not expected.  I don't know if this is the case in Pharo.  I tried switching
from an OrderedCollection to an array but that produced the same results.
