# Day 15: Dueling Generators

This marked my first foray into inheritance in Pharo.  I modeled the generators using a two classes `Day15GeneratorA` and `Day15GeneratorB` that derived from the `Day15Generator`.  All the work is done in the `Day15Generator` class.  The subclasses allowed me to keep configure factors and filters for Generator A and B.

# Solution to Part 1
The `Day15Generator` has two methods to generate values.  First `nextValue` produces the next number.

```smalltalk
Day15Generator >> nextValue
	| nextVal |
	nextVal := (self previousValue * self class factor) rem: self class modulus. 
	self previousValue: nextVal.
	^ nextVal
```
Note the use of `self class factor` to get the factor.  This is where inheritance comes in.  I am new to this, so I'm not sure this is the best way to do it.

Another method, `next16Bits` peels off the 16 bits required by the puzzle solution.

```smalltalk
Day15Generator >> next16Bits
	^ self nextValue bitAnd: self class mask
``` 

From there we just run the generators, comparing the results and counting where they are equal.

```smalltalk
Day15Solver >> solveAwithGeneratorAStart: aStart generatorBStart: bStart
	| generatorA generatorB |
	generatorA := Day15GeneratorA newWithStart: aStart.
	generatorB := Day15GeneratorB newWithStart: bStart.
	^ (1 to: 40000000) count: [ :i | generatorA next16Bits = generatorB next16Bits ]
```

# Solution to Part 2

The solution to part 2 is very similar.  However I use two methods that apply the appropriate filter to the generator values.

```smalltalk
Day15Generator >> nextFilteredValue
	| nextVal |
	nextVal := self nextValue.
	[ self class filter: nextVal ] whileFalse: [ nextVal := self nextValue ].
	^ nextVal
```

```smalltalk
Day15Generator >> nextFiltered16Bits
	^ self nextFilteredValue bitAnd: self class mask
```

Again, I used a class method to differentiate between Generator A and B: `self class filter:`.

We then repeat the same type of solution for the second part of the puzzle.

```smalltalk
Day15Solver >> solveBwithGeneratorAStart: aStart generatorBStart: bStart
	| generatorA generatorB |
	generatorA := Day15GeneratorA newWithStart: aStart.
	generatorB := Day15GeneratorB newWithStart: bStart.
	^ (1 to: 5000000) count: [ :i | generatorA nextFiltered16Bits = generatorB nextFiltered16Bits ]
```