# Day 9: Stream Processing

To solve this puzzle I created a state machine to process the stream of
characters.  I used a Dictionary to select (dispatch) the appropriate method
to handle a character based on the current state of the state machine; the
Dictionary holds blocks for this purpose.  The state is actually a stack so
that when we leave a state we return to the previous state.

There are four states in the state machine: start, group, garbage, and cancel.
I wrote a handler method for each state.  I wrote also methods to enter and
exit each state except for the start state.

The state machine uses instance variables for the state stack, the current
group level, the score, the group count, and the count of garbage characters.

```smalltalk
Day9StateMachine >> initialize
	self state: Stack new.
	self state push: #start.
	self level: 0.
	self garbageCount: 0.
	self groupCount: 0.
	self score: 0.
	self
		dispatcher:
			(Dictionary
				newFromPairs:
					{#garbage.
					[ :c | self handleGarbage: c ].
					#cancel.
					[ :c | self handleCancel: c ].
					#group.
					[ :c | self handleGroup: c ].
					#start.
					[ :c | self handleStart: c ]})
```

The handler methods:

```smalltalk
Day9StateMachine >> handleStart: aChar
	aChar = ${
		ifTrue: [ self enterGroup ].
	aChar = $<
		ifTrue: [ self enterGarbage ].
```

```smalltalk
Day9StateMachine >> handleGroup: aChar
	aChar = ${
		ifTrue: [ self enterGroup ].
	aChar = $}
		ifTrue: [ self exitGroup ].
	aChar = $<
		ifTrue: [ self enterGarbage ].
```

```smalltalk
Day9StateMachine >> handleGarbage: aChar
	aChar = $!
		ifTrue: [ self enterCancel ].
	aChar = $>
		ifTrue: [ self exitGarbage ].
	(aChar ~= $! and: aChar ~= $>)
		ifTrue: [ self incrementGarbageCount ]
```

```smalltalk
Day9StateMachine >> handleCancel: aChar
	self exitCancel
```

The only interesting enter/exit methods are the ones for the `#group` state.
The other enter/exit methods just push or pop a state atom onto/off the
stack.

```smalltalk
Day9StateMachine >> enterGroup
	self state push: #group.
	self level: (self level) + 1
```

```smalltalk
Day9StateMachine >> exitGroup
	self state pop.
	self score: self score + self level.
	self level: self level - 1.
	self groupCount: self groupCount + 1
```

I started by writing a method to handle a single character.  The method
uses the `dispatcher` Dictionary to select a block to run on the character.
The block is determined by the current state.

```smalltalk
Day9StateMachine >> feed: aChar
	(self dispatcher at: self currState) value: aChar
```

After doing that I realized it was more convenient to have a method to process
an entire string.

```smalltalk
Day9StateMachine >> eat: aString
	aString do: [ :c | self feed: c ]
```

The state machine handles all the heavy lifting.  From here the puzzle
solutions are easy.

## Part 1 Solution

For part one, process the input string and then ask the state machine for the
score.

```smalltalk
Day9Solver >> solveA: anInput
	self stateMachine eat: anInput.
	^ self stateMachine score
```

## Part 2 Solution

For part two, process the input string and then ask the state machine for the
count of garbage characters processed.

```smalltalk
Day9Solver >> solveB: anInput
	self stateMachine eat: anInput.
	^ self stateMachine garbageCount
```
