# Day 7: Recursive Circus

The first step in solving this puzzle is to parse the input into a useful
data structure.  In this case, into two useful data structures.  I create
a Dictionary that associates each program with it's parent.  In addition,
I use another Dictionary to form a tree data structure.  Each Dictionary 
entry is keyed by the program name.  The Dictionary entry is a Day7Program
object.  The Day7ProgramObject allowed me to bundle the program name, program
weight, children, and the weight of the children into a single data structure.
Here is the code that parses the input into the two data structures, which are
instance attributes of the Day7Solver.

```smalltalk
Day7Solver >> parseInput: anInput
	| lines |
	lines := anInput findTokens: String cr.
	lines
		do: [ :line | 
			| tokens nm wt |
			tokens := line findTokens: ' '.
			nm := tokens at: 1.
			wt := (tokens at: 2) asInteger.
			tokens size < 3
				ifTrue: [ tree at: nm put: (Day7Program with: nm weight: wt) ]
				ifFalse: [ | ch |
					ch := tokens collect: [ :t | t withoutSuffix: ',' ] from: 4 to: tokens size.
					tree at: nm put: (Day7Program with: nm weight: wt children: ch).
					ch do: [ :child | parents at: child put: nm ] ] ]
```

## Solution to Part 1

Now that we have useful data structures we can solve the puzzle.  The first
part is easy.  Using the tree Dictionary we can get a list of all the programs.
Then we simply look for the program that doesn't appear in the parent Dictionary.
As this is the root of the tree, the method is called getRoot.

```smalltalk
Day7Solver >> getRoot
	| allProgs childProgs |
	allProgs := tree keys.
	childProgs := parents keys.
	^ (allProgs difference: childProgs) at: 1
```

```smalltalk
Day7Solver >> solveA: anInput
	self parseInput: anInput.
	^ self getRoot
```

## Solution to Part 2

The second part of the puzzle is a little trickier because it involves
recursion.  To solve the puzzle I simply recursively walk the tree summing the
weight of the children.  When I find the program with unbalanced children it is
simply a matter of identifying the offending child (using the
findIndexOfDifferenValue method) and changing the weight to balance the tree.
After doing so, the program and new weight are recorded in an instance variable
for later retrieval.

```smalltalk
Day7Solver >> computeChildWeights
	self computeChildWeights: self getRoot.
```

```smalltalk
Day7Solver >> computeChildWeights: aProgName
	| aProg myChildWeights |
	aProg := self tree at: aProgName.
	aProg children size > 0
		ifTrue: [ myChildWeights := aProg children collect: [ :aChildName | self computeChildWeights: aChildName ].
			myChildWeights asSet size = 1
				ifFalse: [ | diffIndex diffName diffWeight newWeight offset delta |
					diffIndex := self findIndexOfDifferentValue: myChildWeights.
					diffIndex = 1
						ifTrue: [ offset := 1 ]
						ifFalse: [ offset := -1 ].
					delta := (myChildWeights at: diffIndex + offset) - (myChildWeights at: diffIndex).
					diffName := aProg children at: diffIndex.
					diffWeight := (tree at: diffName) weight.
					newWeight := diffWeight + delta.
					self changedProgram: diffName.
					self changedProgramWeight: newWeight.
					(tree at: diffName) weight: newWeight.
					myChildWeights at: diffIndex put: (myChildWeights at: diffIndex) + delta ].
			^ aProg weight + myChildWeights sum ]
		ifFalse: [ ^ aProg weight ]
```

```smalltalk
Day7Solver >> findIndexOfDifferentValue: aCollection
	aCollection size = 2
		ifTrue: [ ^ 1 ].
	1 to: aCollection size - 1 do: [ :index | 
		(aCollection at: index) = (aCollection at: index + 1)
			ifFalse: [ index = 1
					ifTrue: [ (aCollection at: index) = (aCollection at: index + 2)
							ifTrue: [ ^ 2 ]
							ifFalse: [ ^ 1 ] ]
					ifFalse: [ ^ index + 1 ] ] ].
	^ 0
```

```smalltalk
Day7Solver >> solveB: anInput
	self parseInput: anInput.
	self computeChildWeights.
	^ self changedProgramWeight
```

Again I noticed that the testing facilities of Pharo are quite helpful.  I was
able to test each method individually and then build them up with confidence
to the solution.

