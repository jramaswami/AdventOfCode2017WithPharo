# Day 14: Disk Defragmentation

Day 14's puzzle reuses the `Day10KnotHash` class to populate the disk we have to defragment.  This is necessary for both parts of the puzzle.  To do this I have the following method:

```smalltalk
Day14Solver >> mapMemory: anInput
	self
		memory:
			((0 to: 127)
				collect: [ :i | 
					| knotHash h s |
					knotHash := Day10KnotHash new.
					s := anInput , '-' , i asString.
					h := (knotHash fullHash: s) asArray.
					(h collect: [ :a | self hexCharAsBinaryString: a ]) reduce: [ :a :b | a , b ] ])
```

This creates a list of bitstrings that represent the values on the disk/memory.

## Solution to Part 1

Once we have a representation of the memory, we can simply count the `1` characters to get the solution to part one.  This is done by row since each row is a string of bits.

```smalltalk
Day14Solver >> solveA: anInput
	self mapMemory: anInput.
	^ (self memory collect: [ :row | row count: [ :b | b = $1 ] ]) sum
```

## Solution to Part 2

Part two is a little more complicated.  It is a BFS just like Day 12's puzzle.  I used the `Point` class to represent the current location for the BFS.  I created my own method to get the four neighbors (up, down, left, right) of the given location.  This method uses the `Point >> fourNeighbors` but makes sure we don't go out of the bounds of memory; *we wouldn't want our computer to crash!*  

```smalltalk
Day14Solver >> fourNeighbors: aPoint
	^ aPoint fourNeighbors select: [ :neighbor | (neighbor x > 0 and: neighbor x < 129) and: (neighbor y > 0 and: neighbor y < 129) ]
```

Just like Day 12 part 2, we will BFS from every unvisited node.  Each time we do, that is a region.

```smalltalk
Day14Solver >> solveB: anInput
	| regionCount |
	self mapMemory: anInput.
	regionCount := 0.
	self bfsVisited: (Matrix rows: 128 columns: 128 element: false).
	self memory
		withIndexDo: [ :row :rindex | 
			row
				withIndexDo: [ :col :cindex | 
					(col = $1 and: (self bfsVisited at: rindex at: cindex) = false)
						ifTrue: [ regionCount := regionCount + 1.
							self bfsFrom: rindex @ cindex ] ] ].
	^ regionCount
```