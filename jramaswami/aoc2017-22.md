# Day 22: Sporifica Virus

When I originally did the puzzle in Python I used namedtuples to represent the position and direction of the virus.  I also used classes to represent the virus and the cluster.  These two decisions meant that my solutions took a couple of minutes to complete.

When I looked at the solution thread on Reddit I learned something new.  You can use complex numbers to represent the position and direction!  I rewrote my Python solutions and they took mere seconds to complete.

I then moved on to rewriting the solutions in Pharo, using complex numbers.  I had to install the Math-Complex package to do so, but no problem.  The solution to Part A worked just fine.  The solution to Part B on the other hand ... it ran and ran and ran until I gave up.  Obviously not the way to go.  

I thought about it and realized that complex number run faster in Python because they are built in; my namedtuples are not.  So I went looking for a similar built in and realized that I could use the Point class.  From there the solutions are pretty straight forward -- you just have to follow the directions given in the problem statement.

## Solution to Part 1

First, we have to parse the grid:

```smalltalk
Day22Solver >> parseGrid: anInput
	| grid |
	grid := Dictionary new.
	anInput lines
		withIndexDo: [ :line :row | 
			line
				withIndexDo: [ :char :col | 
					char = $#
						ifTrue: [ | posn |
							posn := (row * -1) @ col.
							grid at: posn put: char ] ] ].
	^ grid
```

Then we can simulate the virus' movements and infection for the required time, or number of bursts.

```smalltalk
Day22Solver >> solveA: anInput bursts: aNumber
	| grid center virusPosn virusDirn infectionCount |
	infectionCount := 0.
	grid := self parseGrid: anInput.
	center := (anInput lines size quo: 2) + 1.
	virusPosn := (-1 * center) @ center.
	virusDirn := 1 @ 0.
	(1 to: aNumber)
		do: [ :i | 
			| currState |
			currState := grid at: virusPosn ifAbsent: $..
			currState = $.
				ifTrue: [ "clean"
					virusDirn := virusDirn leftRotated.	"rotate left"
					infectionCount := infectionCount + 1.
					grid at: virusPosn put: $#	"infect" ].
			currState = $#
				ifTrue: [ "infected"
					virusDirn := virusDirn rightRotated.	"rotate right"
					grid at: virusPosn put: $.	"clean" ].
			virusPosn := virusPosn + virusDirn ].
	^ infectionCount
```

## Solution to Part 2

The same is true of the solution to part 2.  Just make the necessary changes to the virus behavior and simulate the virus for the appropriate number of bursts.

```smalltalk
Day22Solver >> solveB: anInput bursts: aNumber
	| grid center virusPosn virusDirn infectionCount |
	infectionCount := 0.
	grid := self parseGrid: anInput.
	center := (anInput lines size quo: 2) + 1.
	virusPosn := (-1 * center) @ center.
	virusDirn := 1 @ 0.
	(1 to: aNumber)
		do: [ :i | 
			| currState |
			currState := grid at: virusPosn ifAbsent: $..
			currState = $.
				ifTrue: [ "clean"
					virusDirn := virusDirn leftRotated.	"rotate left"
					grid at: virusPosn put: $w	"weaken" ].
			currState = $w
				ifTrue: [ "weakened; no rotation"
					grid at: virusPosn put: $#.	"infect"
					infectionCount := infectionCount + 1 ].
			currState = $#
				ifTrue: [ "infected"
					virusDirn := virusDirn rightRotated.	"rotate right"
					grid at: virusPosn put: $f	"flag" ].
			currState = $f
				ifTrue: [ "flagged"
					virusDirn := virusDirn * -1.	"rotate 180 degrees"
					grid at: virusPosn put: $.	"clean" ].
			virusPosn := virusPosn + virusDirn ].
	^ infectionCount
```