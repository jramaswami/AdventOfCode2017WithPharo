# Day 12: Digital Plumber

Day 12's puzzle is just a graph problem.  Breadth first search will solve both parts of the puzzle.

First though, we have to parse the input into a graph, using an adjacency list.

```smalltalk
Day12Solver >> parseGraph: anInput
	anInput
		linesDo: [ :line | 
			| tokens node neighbors |
			tokens := line findTokens: ' ,'.
			node := (tokens at: 1) asInteger.
			neighbors := (tokens copyFrom: 3 to: (tokens size)) collect: [ :t | t asInteger].
			self graph at: node put: neighbors.]
```

## Solution to Part 1

To solve the first part of the puzzle, just do a BFS from node 0 where the BFS returns the number of visited nodes.

```smalltalk
Day12Solver >> solveA: anInput
	self parseGraph: anInput.
	self bfsVisited: Dictionary new.
	^ self bfsFrom: 0
```

```smalltalk
Day12Solver >> bfsFrom: aNode
	| queue theNode componentCount |
	queue := DoubleLinkedList new.
	self bfsVisited at: aNode put: true.
	queue addLast: aNode.
	componentCount := 0.
	[ queue isEmpty ]
		whileFalse: [ theNode := queue first.
			queue removeFirst.
			componentCount := componentCount + 1.
			(self graph at: theNode)
				do: [ :neighbor | 
					(self bfsVisited at: neighbor ifAbsent: false)
						ifFalse: [ self bfsVisited at: neighbor put: true.
							queue addLast: neighbor ] ] ].
	^ componentCount
```

Note that I used a DoubleLinkedList to be a queue since we need to push on the right and pop on the left.

## Solution to Part 2

The solution to part 2 requires a BFS from every non-visited node, starting with 0.  If we count the nodes we BFS from, that is the answer to part 2.

```smalltalk
Day12Solver >> solveB: anInput
	| groupCount |
	groupCount := 0.
	self parseGraph: anInput.
	self bfsVisited: Dictionary new.
	self graph
		keysDo: [ :node | 
			(bfsVisited at: node ifAbsent: false)
				ifFalse: [ groupCount := groupCount + 1.
					self bfsFrom: node ] ].
	^ groupCount
```