# Day 13: Packet Scanners

Day 13 was another very straight forward puzzle.  The key insight was to compute when you would get caught rather than simulate it.  A scanner has a period, `p` of  `(d - 1) * 2` where `d` is the scanner's depth.  Since we are always at `0` in the scanner's range, we are caught at time `t` if `0 = t (mod p)`.  We travel at a speed of one layer per picosecond so `t` is equal to the scanner's depth.

## Solution to Part 1

The first step is parsing the puzzle input.  I parsed the input into a Dictionary where the key is the scanner's depth and the value is the scanner's range.

```smalltalk
Day13Solver >> parseInput: anInput
	self scanners: Dictionary new.
	anInput
		linesDo: [ :line | 
			| tokens |
			tokens := line findTokens: ' :'.
			self scanners at: (tokens at: 1) asInteger put: (tokens at: 2) asInteger ]
```

Now all there is to do is go through the scanners, see if we get caught, and if so add the penalty to the total severity.  Note that I ignore depth 0 since we are definitely caught there, but there is no penalty because the depth is 0.

```smalltalk
Day13Solver >> solveA: anInput
	| severity |
	self parseInput: anInput.
	severity := 0.
	self scanners
		keysAndValuesDo: [ :depth :scanRange | 
			depth = 0
				ifFalse: [ | period |
					period := (scanRange - 1) * 2.
					(depth rem: period) = 0
						ifTrue: [ severity := severity + (depth * scanRange) ] ] ].
	^ severity
```

## Solution to Part 2

I solved the second part with two methods.  Since we are looking to see if a given offset allows us to safely cross the firewall, I have method that determines that.

```smalltalk
Day13Solver >> isSafeOffset: anOffset
	self scanners
		keysAndValuesDo: [ :depth :scanRange | 
			| period |
			period := (scanRange - 1) * 2.
			((depth + anOffset) rem: period) = 0
				ifTrue: [ ^ false ] ].
	^ true
```

Note that for this part depth 0 cannot be ignored.  We are interested in finding out if we get caught, not the penalty.

Once we have a method of determining if a given offset is safe, we simply loop until we find a safe offset, returning the safe offset when we find it.

```smalltalk
Day13Solver >> solveB: anInput
	| anOffset |
	self parseInput: anInput.
	anOffset := 1.
	[ true ]
		whileTrue: [ (self isSafeOffset: anOffset)
				ifTrue: [ ^ anOffset ]
				ifFalse: [ anOffset := anOffset + 1 ] ]
```