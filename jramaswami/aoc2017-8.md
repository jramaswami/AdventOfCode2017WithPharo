# Day 8: I Heard You Like Registers

The crux of this problem is to be able to parse the instructions
and carry them out.  The basic data structure used is a Dictionary
to hold the registers.  The `Dictionary at: ifAbsent:` method is
very handy when it comes to incrementing and decrementing registers
that have not been seen before.

I used blocks to represent the increment/decrement operations as well as
the comparison operations.  Then it was a matter of just invoking the
appropriate block when necessary.

The top level is to process the entire output:

```smalltalk
Day8Solver >> processInput: anInput
	(anInput findTokens: String cr) do: [ :line | self processLine: line ]
```

A separate method is used to process each individual line:

```smalltalk
Day8Solver >> processLine: aLine
	| tokens incReg incVal incOp predReg predOp predVal |
	tokens := aLine findTokens: ' '.
	incReg := tokens at: 1.
	incVal := (tokens at: 3) asInteger.
	incOp := (tokens at: 2) = 'inc'
		ifTrue: [ [ self incrementRegister: incReg by: incVal ] ]
		ifFalse: [ [ self decrementRegister: incReg by: incVal ] ].
	predReg := tokens at: 5.
	predVal := (tokens at: 7) asInteger.
	predOp := [ (self predicateOperators at: (tokens at: 6)) value: (registers at: predReg ifAbsent: 0) value: predVal ].
	predOp value
		ifTrue: [ incOp value ]
```

Notice that this uses the Dictionary `predicatOperators` to determine which
block to use for the comparison.  This Dictionary is initialized in the 
`DaySolver >> initialize` method.

```smalltalk
Day8Solver >> initialize
	self registers: Dictionary new.
	self highWaterMark: 0.
	self
		predicateOperators:
			(Dictionary
				newFromPairs:
					{'=='.
					[ :a :b | a = b ].
					'!='.
					[ :a :b | a ~= b ].
					'<'.
					[ :a :b | a < b ].
					'>'.
					[ :a :b | a > b ].
					'<='.
					[ :a :b | a <= b ].
					'>='.
					[ :a :b | a >= b ]})
```

Separate methods are used to increment/decrement the registers:

```smalltalk
Day8Solver >> incrementRegister: aRegister by: aValue
	self registers at: aRegister put: (self registers at: aRegister ifAbsent: 0) + aValue.
	self highWaterMark: (self highWaterMark max: (self registers at: aRegister))
```

```smalltalk
Day8Solver >> decrementRegister: aRegister by: aValue
	self registers at: aRegister put: (self registers at: aRegister ifAbsent: 0) - aValue.
	self highWaterMark: (self highWaterMark max: (self registers at: aRegister))
```

With the base formed we can move on to solving the puzzle.

## Solution to Part 1

The solution to part 1 is straightforward from here.  After processing the
input, just ask for the maximum value in the registers:

```
Day8Solver >> solveA: anInput
	self processInput: anInput.
	^ self registers values max
```

## Solution to Part 2

The solution to part 2 requires that we kept track of the maximum value seen
when incrementing and decrementing the registers.  I called this the
`highWaterMark` and you can see it in the last lines of the `decrementRegister:
by:` and the `incrementRegister: by:` methods.

```smalltalk
Day8Solver >> solveB: anInput
	self processInput: anInput.
	^ self highWaterMark
```

# Observations

I have noticed as I do each challenge that my programming style is changing.  I
am using smaller methods that are individually tested.  I know this is the
Smalltalk style but it happened organically, without my intention.  I find that
interesting.  One of the things I like most about solving competitive
programming problems in Python is the ability to unit test as I go.  Smalltalk
is able to do that with far better facility than even Python does.  I find that
very useful.
