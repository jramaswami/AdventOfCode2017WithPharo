# Day 10: Knot Hash

To solve Day 10, I created a class to represent the Knot Hash described in the problem statement.

## Part 1 Solution

For the first part of the puzzle we were to perform one round of the Knot Hash.  This is stored in the `rawHash` instance attribute of the `Day10KnotHash`.  Later we do more operations on the hash, but each round of the "pinching" operates on the `rawHash`.  My test for the single round of "pinching" is:

```smalltalk
Day10KnotHashTest >> testHashRoundWith
	self knotHash: (Day10KnotHash newFrom: 0 to: 4).
	self knotHash hashRoundWith: #(3 4 1 5).
	self assert: self knotHash rawHash equals: #(3 4 2 1 0).
	self assert: self knotHash currPos equals: 5.
	self assert: self knotHash skipSize equals: 4
```

The `Day10KnotHash` class performs the round:

```smalltalk
Day10KnotHash >> hashRoundWith: lengthsList
	lengthsList
		do: [ :length | 
			self reverseFrom: self currPos toExclusive: self currPos + length.
			self incrementCurrPosBy: (self skipSize + length).
			self incrementSkipSize ]
```

This makes use of several helper methods.  First `reverseFrom: toExclusive:`, which reverses the elements in the hash sequence for the given range of indices.

```smalltalk
Day10KnotHash >> reverseFrom: aStart toExclusive: anEnd
	| subarray |
	subarray := self extractFrom: aStart toExclusive: anEnd.
	self replaceFrom: aStart toExclusive: anEnd with: subarray reverse
```

This in turn makes use of two more helper methods: `extractFrom: toExclusive:` and `replaceFrom: toExclusive: with:`.  The first of these copys the elements of the array between the given range of indices.  The second will replace the elements in the sequence for the given range of indices.

```smalltalk
Day10KnotHash >> extractFrom: aStart toExclusive: anEnd
	anEnd <= ((self rawHash size) + 1)
		ifTrue: [ ^ (self rawHash copyFrom: aStart to: anEnd - 1) ]
		ifFalse: [ | toSelect selectedAtEnd newEnd frontRange backRange |
			toSelect := anEnd - aStart.
			selectedAtEnd := rawHash size - aStart + 1.
			newEnd := toSelect - selectedAtEnd.
			frontRange := self rawHash copyFrom: aStart to: self rawHash size.
			backRange := self rawHash copyFrom: 1 to: newEnd.
			^ frontRange , backRange ]
```

```smalltalk
Day10KnotHash >> replaceFrom: aStart toExclusive: anEnd with: aCollection
	anEnd <= ((self rawHash size) + 1)
		ifTrue: [ aCollection withIndexDo: [ :val :i | self rawHash at: i + aStart - 1 put: val ] ]
		ifFalse: [ | toSelect selectedAtEnd newEnd frontRange backRange |
			toSelect := anEnd - aStart.
			selectedAtEnd := rawHash size - aStart + 1.
			newEnd := toSelect - selectedAtEnd.
			frontRange := aCollection copyFrom: 1 to: selectedAtEnd.
			backRange := aCollection copyFrom: selectedAtEnd + 1 to: aCollection size.
			self replaceFrom: aStart toExclusive: (self rawHash size) + 1 with: frontRange.
			self replaceFrom: 1 toExclusive: 1 + backRange size with: backRange ]
```

Most of the complexity of the methods above stems from the fact that the range to be selected can overlap from back to front of the sequence.

Back to `hashRoundWith:`, there are a couple more helper functions to increment
the current position and skip size.

```smalltalk
Day10KnotHash >> incrementCurrPosBy: aValue
	self currPos: self currPos + (aValue rem: self rawHash size).
	self currPos > self rawHash size
		ifTrue: [ self currPos: self currPos - self rawHash size ]
```

```smalltalk
Day10KnotHash >> incrementSkipSize
	self skipSize: self skipSize + 1
```

Now that we have completed one round of the knot hash, we can get our answer for the first part of the puzzle.

```smalltalk
Day10Solver >> solveA: anInput
	self knotHash hashRoundWith: (self knotHash dataToArray: anInput).
	^ (self knotHash rawHash at: 1) * (self knotHash rawHash at: 2)
```

The `knotHash` is an instance attribute holding an instance of the Day10KnotHash.  Note the use of the `dataToArray`, a method parse the input.

```smalltalk
Day10KnotHash >> dataToArray: anInput
	^ ((anInput findTokens: ',') collect: [ :i | i asInteger ]) asArray
```

So, that is part 1 solved.  Now on to part 2 ...

## Solution to Part 2

We now have to treat our input data differently.  First we must convert the sequence to ASCII and then pad it with [17, 31, 73, 47, 23].  I did this in two stages:

```smalltalk
Day10KnotHash >> dataToAscii: anInput
	^ (anInput asArray collect: [ :c | c asciiValue ])
```

```smalltakl
Day10KnotHash >> dataToPaddedAscii: anInput
	^ (self dataToAscii: anInput), #(17 31 73 47 23)
```

The full hashing of 64 rounds is now done.  After that, the raw hash is condensed according to the instructions in the problem statement and the full hash is returned.

```smalltalk
fullHash: anInput
	| lengths hash |
	lengths := self dataToPaddedAscii: anInput.
	1 to: 64 do: [ :i | self hashRoundWith: lengths ].
	hash := (1 to: 255 by: 16)
		collect: [ :start | 
			| end |
			end := start + 15.
			(self rawHash copyFrom: start to: end) reduce: [ :a :b | a bitXor: b ] ].
	^ ((hash collect: [ :i | i printStringBase: 16 length: 2 padded: true]) reduce: [ :a :b | a , b ]) asLowercase
```

The tests for this are:

```smalltalk
Day10KnotHashTest >> testFullHash
	self knotHash: Day10KnotHash new.
	self assert: (knotHash fullHash: '') equals: 'a2582a3a0e66e6e86e3812dcb672a272'.
	self knotHash: Day10KnotHash new.
	self assert: (knotHash fullHash: 'AoC 2017') equals: '33efeb34ea91902bb2f59c9920caa6cd'.
	self knotHash: Day10KnotHash new.
	self assert: (knotHash fullHash: '1,2,3') equals: '3efbe78a8d82f29979031a4aa0b16a9d'.
	self knotHash: Day10KnotHash new.
	self assert: (knotHash fullHash: '1,2,4') equals: '63960835bcdc130f0b66d7ff4f6a5a8e'.
```

Now that we have the full hashing we can solve the second part of the puzzle, which is just the full hash on the puzzle input.

```smalltalk
Day10Solver >> solveB: anInput
	^ self knotHash fullHash: anInput
```