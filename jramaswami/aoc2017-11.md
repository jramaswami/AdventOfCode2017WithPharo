# Day 11: Hex Ed

This puzzle required me to do a little research.  I found a great [article][1] on how to handle hex coordinates by Red Blob Games.  This was the key to my being able to solve the puzzle.

First I created a class to represent hex coordinates.  The class has instance variables for the `x`, `y`, and `z` coordinates.  There are also two important methods.  First, `+`, to allow addition of coordinates.  This is used to move in a given direction.  Second, `distanceTo:` to compute the distances between two hex coordinates.

```smalltalk
Day11HexCoordinate >> + aHexCoordinate
	^ Day11HexCoordinate newFromX: (self x + aHexCoordinate x) y: (self y + aHexCoordinate y) z: (self z + aHexCoordinate z)
```

```smalltalk
Day11HexCoordinate >> distanceTo: aHexCoordinate
	^ (((self x - aHexCoordinate x) abs) max: ((self y - aHexCoordinate y) abs)) max: ((self z - aHexCoordinate z) abs)
```

See the article mentioned if you don't understand how this works.

Now the puzzle input is a bunch of directions so we will need to be able to turn those into hex coordinates.

```smalltalk
Day11HexCoordinate class >> newFromDirection: aString
	aString = 'n'
		ifTrue: [ ^ Day11HexCoordinate north ].
	aString = 's'
		ifTrue: [ ^ Day11HexCoordinate south ].
	aString = 'ne'
		ifTrue: [ ^ Day11HexCoordinate northEast ].
	aString = 'nw'
		ifTrue: [ ^ Day11HexCoordinate northWest ].
	aString = 'se'
		ifTrue: [ ^ Day11HexCoordinate southEast ].
	aString = 'sw'
		ifTrue: [ ^ Day11HexCoordinate southWest ]
```

There are also class method to make each direction.  For example:

```Day11HexCoordinate class >> northEast
	^ self newFromX: 1 y: 0 z: -1
```

## Solution to Part 1

Once we have a working coordinate system and a way to read the file, the solution is pretty straight forward.

```smalltalk
Day11Solver >> solveA: anInput
	| movements finalPos |
	movements := (anInput findTokens: ',') collect: [ :d | Day11HexCoordinate newFromDirection: d ].
	movements addFirst: self origin.
	finalPos := movements reduce: [ :a :b | a + b ].
	^ self origin distanceTo: finalPos
```

First, parse the input into `movements`.  Put the origin at the beginning of the sequence of movements.  We can then use `reduce` to move from the origin for each of the movements given in the input, giving us the final position.  The solution then, is the distance between the origin and the final position.

## Solution to Part 2

Our coordinate system allows us to solve the second part of the puzzle fairly easily too.

```smalltalk
Day10Solver >> solveB: anInput
	| movements positions distances currPos |
	movements := (anInput findTokens: ',') collect: [ :d | Day11HexCoordinate newFromDirection: d ].
	currPos := self origin.
	positions := movements
		collect: [ :move | 
			currPos := currPos + move.
			currPos ].
	distances := positions collect: [ :pos | self origin distanceTo: pos ].
	^ distances max
```

Starting off with parsing our movements again, we will then `collect` each position to which we moved.  We can then collect the `distances` between the origin and each of the `positions`.  We then return the max from `distances`.


[1]: https://www.redblobgames.com/grids/hexagons/