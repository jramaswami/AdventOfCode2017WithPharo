# Day 3: Spiral Memory

## Solution to Part 1

This was an interesting problem to solve in Pharo.  I started out with a 
pretty ugly solution but I was able to improve it incrementally due to the
refactoring and testing facilities in Pharo.  In addition, the built-in
Point class came in handy especially because I was able to add to that class
a method to compute the Manhattan distance between two points.

The Day3Solver class has three instance variables: spiral, index, and location.
Starting at the bottom, I created four methods to move from one location
to another, moveUp, moveDown, moveLeft, and moveRight.

```smalltalk
Day3Solver >> moveUp
	self location: self location + (-1 @ 0).
	self index: self index + 1
```

Once we are moving locations, we will want to fill a certain number of
cells in a given direction, until some condition is satisfied, i.e. the
cell index is greater than our puzzle input.

```smalltalk
Day3Solver >> fill: aBlock times: aDelta unless: unlessBlock
	1 to: aDelta do: [ :i | 
		(unlessBlock value)
			ifTrue: [ ^ i ].
		aBlock value ]
```

I used blocks for both the condition to be satisfied and how to fill the cells.
This is handy because it can be reused for the second part of the puzzle.  The
method used to fill the cell for the first part of the puzzle is:

```smalltalk
Day3Solver >> recordIndex
	(self spiral) at: (self index) put: (self location)  
```

This will fill the spiral at the current location with the current index.

Putting it all together:

```smalltalk
Day3Solver >> solveA: input
	| delta |
	(self spiral) at: (self index) put: (self location).
	delta := 2.
	[ (self index) < input ]
		whileTrue: [
			self fill: [ self moveRight. self recordIndex ] times: 1 unless: [ (self index) > input ].  
			self fill: [ self moveUp. self recordIndex ] times: (delta - 1) unless: [ (self index) > input ].
			self fill: [ self moveLeft. self recordIndex ] times: delta unless: [ (self index) > input ].
			self fill: [ self moveDown. self recordIndex ] times: delta unless: [ (self index) > input ].
			self fill: [ self moveRight. self recordIndex ] times: delta unless: [ (self index) > input ].
			delta := delta + 2 ].
		
	^ (0 @ 0) manhattanDistance: (spiral at: input).
```

Note the manhattanDistance method.  This is what I added to the existing Point
class.

```smalltalk
Point >> manhattanDistance: otherPoint
	^ ((self x - otherPoint x) abs) + ((self y - otherPoint y) abs)
```

## Solution to Part 2

The solution to Part 2 reused many of the methods from Part 1.  From the top
this time:

```smalltalk
Day3Solver >> solveB: input
	| delta |
	(self spiral) at: (self location) put: 1.
	delta := 2.
	[ ((self spiral) at: (self location)) <= input ]
		whileTrue: [ 
		    self
				fill: [ self moveRight.
					self recordStressValue ]
				times: 1
				unless: [ ((self spiral) at: (self location))  > input ].
			self
				fill: [ self moveUp.
					self recordStressValue ]
				times: delta - 1
				unless: [ ((self spiral) at: (self location))  > input ].
			self
				fill: [ self moveLeft.
					self recordStressValue ]
				times: delta
				unless: [ ((self spiral) at: (self location))  > input ].
			self
				fill: [ self moveDown.
					self recordStressValue ]
				times: delta
				unless: [ ((self spiral) at: (self location))  > input ].
			self
				fill: [ self moveRight.
					self recordStressValue ]
				times: delta
				unless: [ ((self spiral) at: (self location))  > input ].
			delta := delta + 2 ].
	^ (self spiral) at: (self location)
```

Note this time we use the recordStressValue to fill the spiral.  Note also
that the spiral is keyed on the location instead of the index.

```smalltalk
Day3Solver >> recordStressValue
	| stressValue |
	stressValue := 0.
	(self location) eightNeighbors do: [ :neighbor |
		 stressValue := stressValue + ((self spiral) at: neighbor ifAbsent: 0) ].
	(self spiral) at: (self location) put: stressValue
```

The one thing I couldn't figure out how to do was to make the filling of the
spiral completely generic.  I wanted to pass in a block like `[self
recordStressValue]` or `[self recordIndex]` to a method that would fill the
spiral completely.  But I couldn't figure out how to do that because it seems
that it would require a block in a block, e.g. `[self moveRight.
recordSomethingBlock]`.
