# Day 4
The source code related to this solution is available at https://github.com/juliendelplanque/AOC2017.

## Solution of puzzle 1
In this problem, a list of strings containing words with space character in between is provided. The problem consists in deciding if this string represent a good passphrase or not.
A good passphrase is defined as a phrase in which each word appears only once.

To solve this problem, I create a `AOCSolver4` object. Which will provide a `#solve` method to solve the problem according to the strings list in `#data` instance variable.
I also create a `#puzzleData` methods which hold my raw puzzle input. If the `#data` instance variable is not initialized, the list of strings extracted from `#puzzleData` is returned.

```
AOCSolver2>>#puzzleData
	^ 'pphsv ojtou brvhsj cer ntfhlra udeh ccgtyzc zoyzmh jum lugbnk
vxjnf fzqitnj uyfck blnl impo kxoow nngd worcm bdesehw
caibh nfuk kfnu llfdbz uxjty yxjut jcea
qiho qif eupwww avyglnj nxzotsu hio lws
...
```


```
AOCSolver2>>#data
	^ data ifNil: [ self puzzleData lines ]
```


```
AOCSolver2>>#data: anObject
	data := anObject
```

Some examples of inputs/outputs for this puzzle are provided. So I create unit tests from them in `AOCSolver4Test`:

```
AOCSolver4Test>>#setUp
	solver := AOCSolver4 new
```

```
AOCSolver4Test>>#testSolve
	solver data: #('aa bb cc dd ee').

	self assert: solver solve equals: 1
```

```
AOCSolver4Test>>#testSolve2
	solver data: #('aa bb cc dd aa').

	self assert: solver solve equals: 0
```

```
AOCSolver4Test>>#testSolve3
	solver data: #('aa bb cc dd aaa').

	self assert: solver solve equals: 1
```

Now we have our unit tests, we can implement the `#solve` method of `AOCSolver4`.
My implementation is the following:

```
AOCSolver2>>#solve
	^ (self data select: [ :line |
		|splitted|
		splitted := line splitOn: Character space.
		splitted size = splitted asSet size ]) size
```

Which makes my unit tests green and, with my puzzle input, return the number `466`.

## Solution of puzzle 2
The second problem is similar to the first but a valid passphrase is a passphrase for which each pair of words of the same size do not have the exact same letters.

Some examples of inputs/outputs for this puzzle are provided. So I create unit tests from them in `AOCSolver4Test`:


```
AOCSolver4Test>>#testSolveBis
	solver data: #('abcde fghij').

	self assert: solver solveBis equals: 1
```

```
AOCSolver4Test>>#testSolveBis2
	solver data: #('abcde xyz ecdab').

	self assert: solver solveBis equals: 0
```

```
AOCSolver4Test>>#testSolveBis3
	solver data: #('a ab abc abd abf abj').

	self assert: solver solveBis equals: 1
```

```
AOCSolver4Test>>#testSolveBis4
	solver data: #('iiii oiii ooii oooi oooo').

	self assert: solver solveBis equals: 1
```

```
AOCSolver4Test>>#testSolveBis5
	solver data: #('oiii ioii iioi iiio').

	self assert: solver solveBis equals: 0
```

Now we have our unit tests, we can implement the `#solveBis` method of `AOCSolver4`.
My implementation is the following:

```
AOCSolver4>>#solveBis
	^ (self data select: [ :line |
		|splitted set|
		splitted := line splitOn: Character space.
		set := PluggableSet new
					equalBlock: [ :a :b | a sorted = b sorted ];
					hashBlock: [ :a | a sorted hash ];
					yourself.
		set addAll: splitted.
		set size = splitted size ]) size
```

The trick here is to use a PluggableSet with the equality relation between strings redefined
as in the block `[ :a :b | a sorted = b sorted ]`. The hash of an item of the set is thus defined
as `[ :a | a sorted hash ]`.

This implementation makes my unit tests green and, with my puzzle input, return the number `251`.
