# Day 5
The source code related to this solution is available at https://github.com/juliendelplanque/AOC2017.

## Solution of puzzle 1
In this problem, you receive a list of integers that represents relative distance to which you need to jump in the list.
These distances can be positive (jump to a position forward in the list) or negative (jump to a position backward in the list).
The goal of the first puzzle is to find the number of jump required to jump to a position outside of the list range (i.e.
smaller than 1 or greater than list size). Each time you jumped from a position, the jump value at this position has to be
incremented by 1.

To solve this problem, I create a `AOCSolver5` object. Which will provide a `#solve` method to solve the problem according to the strings list in `#data` instance variable.
I also create a `#puzzleData` methods which hold my raw puzzle input. If the `#data` instance variable is not initialized, the list of strings extracted from `#puzzleData` is returned.

```
AOCSolver5>>#puzzleData
	^ '2
1
2
-1
2
-3
1
-2
...
```

```
AOCSolver5>>#data
	^ data ifNil: [ self puzzleData lines collect: #asNumber ]
```

```
AOCSolver5>>#data: anObject
	data := anObject
```

An example of input/output for this puzzle is provided. So I create unit tests in `AOCSolver5Test`:

```
AOCSolver5Test>>#setUp
	solver := AOCSolver5 new
```

```
AOCSolver5Test>>#testSolve
	solver data: #(0 3  0  1  -3).

	self assert: solver solve equals: 5
```

Now we have our unit tests, we can implement the `#solve` method of `AOCSolver5`.
My implementation is the following:

```
AOCSolver5>>#solve
	| jumps currentPosition jumpCount |
	jumps := self data.
	currentPosition := 1.
	jumpCount := 0.
	[ currentPosition between: 1 and: jumps size ]
		whileTrue: [
			|oldJumpValue|
			oldJumpValue := jumps at: currentPosition.
			jumps at: currentPosition put: (oldJumpValue + 1).
			currentPosition := currentPosition + oldJumpValue.
			jumpCount := jumpCount + 1 ].

	^ jumpCount
```

Which makes my unit test green and, with my puzzle input, returns the number `356945`.

## Solution of puzzle 2
The second problem is similar to the first except that when you jumped from a position, its value has to be incremented by
1 if the jump value is smaller than `3` else it has to be decremented by 1.

An example of input/output for this puzzle is provided. So I create unit tests in `AOCSolver5Test`:
```
AOCSolver5Test>>#testSolveBis
	solver data: #(0 3  0  1  -3).

	self assert: solver solveBis equals: 10
```

Now we have our unit tests, we can implement the `#solveBis` method of `AOCSolver5`.
My implementation is the following:

```
AOCSolver5>>#solveBis
	| jumps currentPosition jumpCount |
	jumps := self data.
	currentPosition := 1.
	jumpCount := 0.
	[ currentPosition between: 1 and: jumps size ]
		whileTrue: [
			|oldJumpValue|
			oldJumpValue := jumps at: currentPosition.
			oldJumpValue  >= 3
				ifTrue: [  jumps at: currentPosition put: (oldJumpValue - 1) ]
				ifFalse: [ jumps at: currentPosition put: (oldJumpValue + 1) ].
			currentPosition := currentPosition + oldJumpValue.
			jumpCount := jumpCount + 1 ].

	^ jumpCount
```

Which is really similar to the first one except the management of the new jump value that change.

Ny unit test is green and, with my puzzle input, returns the number `28372145`.
